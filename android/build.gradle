group 'com.boskokg.flutter_blue_plus'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.3'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 31

    if (project.android.hasProperty("namespace")) {
        namespace 'com.boskokg.flutter_blue_plus'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
    }
    sourceSets {
        main {
            proto {
                srcDir '../protos'
            }
        }
    }
}

protobuf {
    protoc {
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.18.0:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.18.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:3.18.0'
}
